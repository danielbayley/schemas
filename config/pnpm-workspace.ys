!YS-v0:

name     =: fs-filename(FILE "*")
domain   =: "pnpm.io"
branch   =: "main"
repo     =: "https://github.com/pnpm/$domain"
docs     =: "$repo/raw/refs/heads/$branch/docs"
id       =: "$name_yaml"
markdown =: slurp("$docs/$id.md")
metadata =: markdown:lines.nth(2):yaml/load
title    =: &title metadata.title

$schema:: ENV.schema
$id:: "$(ENV.URL)/$name.json"
title: *title
description:: "JSON schema for $title files."
$comment:: "https://$domain/$id"

$defs:
  object:
    type: object
    minProperties: 1
    anchor =:: &object
      $ref: "#/$defs/object"

<<: *object
additionalProperties: false
required: [packages]
properties:
  $schema:
    $comment: https://json-schema.org/understanding-json-schema/reference/schema#schema
    $ref:: "$(ENV.ref)/uri.json"

  remove =: /[^\w.\s\/-]+/

  packages:
    description::
      markdown:lines.subvec(5 9).join("\n").replace(remove) +
      markdown:lines.subvec(23 25).join(" ")
    type: array
    minItems: 1
    uniqueItems: true
    items:
      $ref:: "$(ENV.ref)/paths.json#/\\$defs/folder"

  markdown =: slurp("$docs/catalogs.md")

  catalog:
    $ref:: &dependency "$(ENV.ref)/package.json#/\\$defs/dependency"
    description:: markdown:lines.nth(86).replace(remove)
    $comment:: "https://$domain#default-catalog"

  catalogs:
    $ref: "#/$defs/object"
    description:: markdown:lines.nth(98).replace(remove)
    $comment:: "https://$domain#named-catalogs"
    additionalProperties:
      $ref: *dependency
